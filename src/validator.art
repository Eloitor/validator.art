;===============================================
; Validator.art
;
; String validation library
; for Arturo
;
; MIT License
; (c) 2024 Yanis Zafirópulos
;==========================================================
; @file: src/validator.art
;==========================================================

export module [
    ;---------------------------
    ; Private variables
    ;---------------------------

    plugins: #[]

    ;---------------------------
    ; Initialization
    ;---------------------------

    init: method [][
        loop list ./"plugins" 'plugin [
            name: extract.filename plugin
            \plugins\[name]: # plugin
        ]
    ]

    ;---------------------------
    ; Private methods
    ;---------------------------

    fetchParams: method [usedParams :dictionary][
        res: #[]

        unless empty? usedParams ->
            loop usedParams [pname, pspec][
                switch pval: <= attr pname [
                    ensure -> is? pspec\0 pval
                    res\[pname]: pval
                ][
                    res\[pname]: pspec\1
                ]
            ]

        return res
    ]

    ;---------------------------
    ; Public methods
    ;---------------------------

    valid?: method.public [str :string :block][
        ;; description: « check if given string is valid
        ;; options: [
        ;;      email: « verify e-mail address
        ;;      url: « verify URL
        ;; ]
        ;; returns: :logical
        ;; example: {
        ;;      valid?.url "https://arturo-lang.io"
        ;;      ; => true
        ;;
        ;;      valid?.email "loremIpsum@
        ;;      ; => false
        ;;
        ;;      valid?.email ["my@email.com", "info@google.com"]
        ;;      ; => true
        ;; }
        inputs: (string? str)? -> @[str][
            ensure -> is? [:string] str
            str
        ]
        loop \plugins [name, plugin][
            if attr name [
                prms: \fetchParams plugin\params
                loop inputs 'inp [
                    unless do [ plugin\action inp prms ] ->
                        return false
                ]
            ]
        ]
        return true
    ]
]!
